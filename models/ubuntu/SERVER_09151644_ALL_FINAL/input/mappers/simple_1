ENUM
absin {VALID, INV}
ENUM
abssut {CURRENT, NEXT, ZERO, FRESH}
ENUM
abslearner {CURRENT, NEXT, ZERO, FRESH}

STATE
int learnerSeqProposed = -3;
int sutSeq = -3;
int learnerSeq = -3;

MAP incomingResponse (flags flagsIn, int concSeqIn, int concAckIn
						-> abssut absSeqIn, abslearner absAckIn)
if (concSeqIn == sutSeq+1) {
	absSeqIn = abssut.NEXT;
} else { if (concSeqIn == sutSeq) {
	absSeqIn = abssut.CURRENT;
} else { if (concSeqIn == 0) {
	absSeqIn = abssut.ZERO;
} else {
	absSeqIn = abssut.FRESH;
}}}
if ((concAckIn == learnerSeq+1) | (concAckIn == learnerSeqProposed+1)) {
	absAckIn = abslearner.NEXT;
} else { if (concAckIn == learnerSeq) {
	absAckIn = abslearner.CURRENT;
} else { if (concAckIn == 0) {
	absAckIn = abslearner.ZERO;
} else {
	absAckIn = abslearner.FRESH;
}}}
UPDATE
if ((flagsIn has $R) | ((learnerSeqProposed != -3) & (concAckIn != learnerSeqProposed+1))) {
	// upon reset, or if a fresh seq from the learner is not acknowledged
	sutSeq = -3;
	learnerSeq = -3;
} else { if ((learnerSeqProposed != -3) | (concSeqIn == sutSeq+1)) {
	// if a fresh seq from the learner is acknowledged, or if the sequence number is valid
	sutSeq = concSeqIn;
	learnerSeq = concAckIn;
} else {if (flagsIn has $S) {
	// fresh sequence number
	sutSeq = concSeqIn;
	if (concAckIn == 0) {
		learnerSeq = learnerSeq;
	} else {
		learnerSeq = concAckIn;
	}
} else {
	sutSeq = sutSeq;
	learnerSeq = learnerSeq;
}}}
learnerSeqProposed = -3;

MAP outgoingRequest (int concSeqOut, int concAckOut, flags flagsOut
						-> absin absSeqOut, absin absAckOut, flags flagsOut2)
flagsOut2 = flagsOut;
if ((learnerSeq == -3) | (learnerSeq == concSeqOut)) {
	absSeqOut = absin.VALID;
} else {
	absSeqOut = absin.INV;
}
if (((sutSeq == -3) & (concAckOut == 0)) | ((sutSeq != -3) & (concAckOut == sutSeq+1))) {
	absAckOut = absin.VALID;
} else {
	absAckOut = absin.INV;
}
UPDATE
if (learnerSeq == -3) {
	learnerSeqProposed = concSeqOut;
} else {
	learnerSeqProposed = learnerSeqProposed;
}
sutSeq = sutSeq;
learnerSeq = learnerSeq;

MAP incomingTimeout (int tmp -> int tmp2)
tmp2 = tmp;
UPDATE
learnerSeqProposed = -3;
sutSeq = sutSeq;
learnerSeq = learnerSeq;
